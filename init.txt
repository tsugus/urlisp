;;;;; An Initial Program for "pure LISP" ;;;;;;
;; Version 0.5.3
;; (C) 2024-2025 Tsugu
;;;;;

; -------------------------------------
; Basic Functions

(de caar (x) (car (car x)))
(de cadr (x) (car (cdr x)))
(de cdar (x) (cdr (car x)))
(de cddr (x) (cdr (cdr x)))

(de caaar (x) (car (caar x)))
(de caadr (x) (car (cadr x)))
(de cadar (x) (car (cdar x)))
(de caddr (x) (car (cddr x)))

(de cdaar (x) (cdr (caar x)))
(de cdadr (x) (cdr (cadr x)))
(de cddar (x) (cdr (cdar x)))
(de cdddr (x) (cdr (cddr x)))

(de caddar (x) (car (cddar x)))

(de null (x)
  (eq x '()))

(de not (x)
  (cond (x '())
        (t 't)))

(de and (x y)
  (cond ((null x) '())
        (t (cond (y 't)
                  (t '())))))

(de or (x y)
  (cond (x 't)
        (t (cond ((null y) '())
                  (t 't)))))

;; (rev-append '(a b c) '(d e)) ==> (c b a d e)
;;
(de rev-append (l m)
  (cond ((null l) m)
        (t (rev-append (cdr l) (cons (car l) m)))))

(de reverse (l) (rev-append l '()))

(de append (l m)
  (rev-append (rev-append l '()) m))

; -------------------------------------
; Preparation

;; (assoclist '(x y . z) '(a b c d)) ==> ((x . a) (y . b) (z c d))
;;
(de assoclist (keys values)
  (cond ((or (null keys) (null values)) '())
        ((and (not (atom keys)) (not (atom values)))
         (cons (cons (car keys) (car values))
               (assoclist (cdr keys) (cdr values))))
        ((not (null keys)) (list (cons keys values)))))

;; (assoc 'y '((x . a) (y . b) (z . c))) ==> b
;; (assoc 'w '((x . a) (y . b) (z . c))) ==> ()
;;
(de assoc (key lst)
  (cond ((null lst) '())
        ((eq key (caar lst)) (cdar lst))
        (t (assoc key (cdr lst)))))

; -------------------------------------
; Meta-circular Evaluator

(de isSUBR (x)
  (cond ((eq x 'atom) 't)
        ((eq x 'eq) 't)
        ((eq x 'car) 't)
        ((eq x 'cdr) 't)
        ((eq x 'cons) 't)
        ((eq x 'eval*) 't)
        (t '())))

(de eval_ (exp env)
  (cond
    ((eq exp 't) 't)
    ((atom exp) (assoc exp env))
    ((isSUBR (car exp))
      (apply_ (car exp) (evlist (cdr exp) env) env))
    (t
      (apply_ (car exp) (cdr exp) env))))

(de apply_ (func args env)
  (cond
    ((and (atom func) (not (null func)))
    (cond
      ((eq func 'quote) (car args))
      ((eq func 'atom)  (cond ((atom (car args)) 't)
                              (t '())))
      ((eq func 'eq)    (cond
                          ((eq (car args) (cadr args)) 't)
                          (t '())))
      ((eq func 'car)   (car (car args)))
      ((eq func 'cdr)   (cdr (car args)))
      ((eq func 'cons)  (cons (car args) (cadr args)))
      ((eq func 'cond)  (evcond args env))
      ((eq func 'eval*)  (eval_ (car args) (cadr args)))
      (t (eval_ (cons (assoc func env) args) env))))
    ((eq (car func) 'label)
    (eval_ (cons (caddr func) args)
           (cons (cons (cadr func) (caddr func)) env)))
    ((eq (car func) 'lambda)
    (eval_ (caddr func)
           (append (assoclist (cadr func) (evlist args env))
                   env)))))

;; (evcond '(((atom x) x) (t (car x))) '((x a b c) (t . t)))
;;  ==> a
;;
(de evcond (clauses env)
  (cond ((null clauses) '())
        ((null (eval_ (caar clauses) env))
         (evcond (cdr clauses) env))
        (t (eval_ (cadar clauses) env))))

;; (evlist '(x y) '((x . a) (y . b))) ==> (a b)
;;
(de evlist (members env)
  (cond ((null members) '())
        (t (cons (eval_ (car members) env)
                 (evlist (cdr members) env)))))

; -------------------------------------
; Test 1
'Test_1

;; ==> a
;;
(eval_ 'x '((x . a) (y . b)))

;; ==> t
;;
(eval_ '(eq 'a 'a) '())

;; ==> (a b c)
;;
(eval_ '(cons x '(b c)) '((x . a) (y . b)))

;; ==> list
;;
(eval_ '(cond ((atom x) 'atom)
              (t 'list))
       '((x '(a b))))

;; ==> (a b c)
;;
(eval_ '(f '(b c))
       '((f . (lambda (x) (cons 'a x)))))

;; ==> a
;;
(eval_
 '((label firstatom
          (lambda (x)
            (cond ((atom x) x)
                  (t (firstatom (car x))))))
   y)
 '((y . ((a b) (c d)))))

;; ==> (a c d)
;;
(eval_
 '((lambda (x y) (cons x (cdr y)))
   'a
   '(b c d))
 '())

;; (subst 'm 'b '(a b (a b c) d)) ==> (a m (a m c) d)
;;
(eval_
 '((label subst
          (lambda (x y z)
            (cond ((atom z) (cond ((eq z y) x)
                                   (t z)))
                  (t (cons (subst x y (car z))
                            (subst x y (cdr z)))))))
   'm 'b '(a b (a b c) d))
 '())

; =====================================
; "Meta-circular" Environment List

(setq plisp
'(
  (caar . (lambda (x) (car (car x))))
  (cadr . (lambda (x) (car (cdr x))))
  (cdar . (lambda (x) (cdr (car x))))
  (cddr . (lambda (x) (cdr (cdr x))))
  (caaar . (lambda (x) (car (caar x))))
  (caadr . (lambda (x) (car (cadr x))))
  (cadar . (lambda (x) (car (cdar x))))
  (caddr . (lambda (x) (car (cddr x))))
  (cdaar . (lambda (x) (cdr (caar x))))
  (cdadr . (lambda (x) (cdr (cadr x))))
  (cddar . (lambda (x) (cdr (cdar x))))
  (cdddr . (lambda (x) (cdr (cddr x))))
  (caddar . (lambda (x) (car (cddar x))))
  (null
   .(lambda (x) (eq x '())))
  (not
   .(lambda (x) (cond (x '())
                      (t 't))))
  (and
   . (lambda (x y)
       (cond ((null x) '())
             (t (cond (y 't)
                      (t '()))))))
  (or
   . (lambda (x y)
       (cond (x 't)
             (t (cond ((null y) '())
                      (t 't))))))
  (rev-append
   . (lambda (l m)
       (cond ((null l) m)
             (t (rev-append (cdr l) (cons (car l) m))))))
  (reverse
   . (lambda (l) (rev-append l '())))
  (append
   . (lambda (l m) (rev-append (rev-append l '()) m)))
  (assoclist
   . (lambda (keys values)
       (cond ((or (null keys) (null values)) '())
             ((and (not (atom keys)) (not (atom values)))
              (cons (cons (car keys) (car values))
                    (assoclist (cdr keys) (cdr values))))
             ((not (null keys))
              ((lambda x x) (cons keys values))))))
  (assoc
   . (lambda (key lst)
       (cond ((null lst) '())
             ((eq key (caar lst)) (cdar lst))
             (t (assoc key (cdr lst))))))
  (isSUBR
   . (lambda (x)
       (cond ((eq x 'atom) 't)
             ((eq x 'eq) 't)
             ((eq x 'car) 't)
             ((eq x 'cdr) 't)
             ((eq x 'cons) 't)
             ((eq x 'eval) 't)
             (t '()))))
  (eval
   . (lambda (exp env)
       (cond
         ((eq exp 't) 't)
         ((atom exp) (assoc exp env))
         ((isSUBR (car exp))
          (apply (car exp) (evlist (cdr exp) env) env))
         (t
          (apply (car exp) (cdr exp) env)))))
  (apply
   . (lambda (func args env)
       (cond
         ((and (atom func) (not (null func)))
          (cond
            ((eq func 'quote) (car args))
            ((eq func 'atom)  (cond ((atom (car args)) 't)
                                    (t '())))
            ((eq func 'eq)    (cond
                                ((eq (car args) (cadr args)) 't)
                                (t '())))
            ((eq func 'car)   (car (car args)))
            ((eq func 'cdr)   (cdr (car args)))
            ((eq func 'cons)  (cons (car args) (cadr args)))
            ((eq func 'cond)  (evcond args env))
            ((eq func 'eval)  (eval (car args) (cadr args)))
            (t (eval (cons (assoc func env) args) env))))
         ((eq (car func) 'label)
          (eval (cons (caddr func) args)
                 (cons (cons (cadr func) (caddr func)) env)))
         ((eq (car func) 'lambda)
          (eval (caddr func)
                 (append (assoclist (cadr func) (evlist args env))
                         env))))))
  (evcond
   . (lambda (clauses env)
       (cond ((null clauses) '())
             ((null (eval (caar clauses) env))
              (evcond (cdr clauses) env))
             (t (eval (cadar clauses) env)))))
  (evlist
   . (lambda (members env)
       (cond ((null members) '())
             (t (cons (eval (car members) env)
                      (evlist (cdr members) env))))))
))

; =====================================
; Test 2
; (The "eval" inside "eval_" is different from the outside "eval".)
'Tesr_2

; ==> Error!
(eval '(f '(a b c)) '((f . cdr)))

; ==> (b c)
(eval_ '(eval '(f '(a b c)) '((f . cdr))) '())

; =====================================
; Test 3
; ("eval" is a universal Turing machine.)
'Test_3

;; ==> a
;;
(eval_ '(eval
 '(eval 'x '((x . a) (y . b)))
 plisp) plisp)

;; ==> t
;;
(eval_ '(eval
 '(eval '(eq 'a 'a) '())
 plisp) plisp)

;; ==> (a b c)
;;
(eval_
 '(eval '(cons x '(b c)) '((x . a) (y . b)))
 plisp)

;; ==> list
;;
(eval_ '(eval
 '(eval '(cond ((atom x) 'atom)
                (t 'list))
         '((x '(a b))))
 plisp) plisp)

;; ==> (a b c)
;;
(eval_ '(eval
 '(eval '(f '(b c))
         '((f . (lambda (x) (cons 'a x)))))
 plisp) plisp)

;; ==> a
;;
(eval_ '(eval
 '(eval
   '((label firstatom
            (lambda (x)
              (cond ((atom x) x)
                    (t (firstatom (car x))))))
     y)
   '((y . ((a b) (c d)))))
 plisp) plisp)

;; = (a c d)
;;
(eval_ '(eval
 '(eval
   '((lambda (x y) (cons x (cdr y)))
     'a
     '(b c d))
   '())
 plisp) plisp)

;; (subst 'm 'b '(a b (a b c) d)) => (a m (a m c) d)
;; *** It takes time! ***
;(eval_ '(eval
; '(eval
;   '((label subst
;            (lambda (x y z)
;              (cond ((atom z) (cond ((eq z y) x)
;                                    (t z)))
;                    (t (cons (subst x y (car z))
;                             (subst x y (cdr z)))))))
;     'm 'b '(a b (a b c) d))
;   '())
; plisp) plisp)

; -------------------------------------
